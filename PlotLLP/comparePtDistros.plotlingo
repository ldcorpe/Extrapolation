#
# Make a half-way decent plot comparing the pT distributions for a Lxy region.
# The files must be run by hand to generate the inputs:
#
# .\PlotLLP\bin\x86\Debug\PlotLLP.exe "C:\Users\gordo\Downloads\LLPExtrapolationMCTree-600pi50lt9mW.root"
# .\PlotLLP\bin\x86\Debug\PlotLLP.exe "C:\Users\gordo\Downloads\LLPExtrapolationMCTree-600pi50lt5mW.root"
#
# Do it in the solution directory, and then this should work.
#

NormalizePlots();

sample = "600pi50";

f9 = file("../info-LLPExtrapolationMCTree-600pi50lt9mW.root");
f5 = file("..\info-LLPExtrapolationMCTree-600pi50lt5mW.root");

Legend("lt9" => {Title => "600-60 9m", Color => 1});
Legend("lt5" => {Title => "600-60 5m", Color => 3});

stems = [
	{name => "pTall", title => "All Jets", what => "pt"},
	{name => "pTregionA", title => "All Region A Jets", what => "pt"},
	{name => "Etaall", title => "All Jets", what => "eta"},
	{name => "EtaregionA", title => "All Jets", what => "eta"},
	{name => "pTlxy_2000_3500", title => "Lxy 2.0-3.5 m", what => "pt"},
	{name => "pTlxy_2500_3000", title => "Lxy 2.5-3.0 m", what => "pt"},
	{name => "Etalxy_2000_3500", title => "Lxy 2.0-3.5 m", what => "eta"},
	{name => "Etalxy_2500_3000", title => "Lxy 2.5-3.0 m", what => "eta"},
	{name => "pTAlxy_2000_3500", title => "Region A Lxy 2.0-3.5 m", what => "pt"},
	{name => "pTAlxy_2500_3000", title => "Region A Lxy 2.5-3.0 m", what => "pt"}
];

for (stems) {
	# First, look at the overall pT distributions (they are the same).
	p9_all = (f9.Get("{name}_{what}1") + f9.Get("{name}_{what}2")).tag("lt9").normalize();
	p5_all = (f5.Get("{name}_{what}1") + f5.Get("{name}_{what}2")).tag("lt5").normalize();

	[p9_all,p5_all]
		.plot()
		.title("")
		.Text("{title}")
		.filename("{name}_{sample}");
};

effstems = [
	{title => "pT Efficiency", namedenom => "pTall", namenum => "pTregionA", what => "pt"},
	{title => "eta Efficiency", namedenom => "Etaall", namenum => "EtaregionA", what => "eta"},
	{title => "Lxy 2.0-3.5 m", namedenom => "pTlxy_2000_3500", namenum => "pTAlxy_2000_3500", what => "pt"},
	{title => "Lxy 2.0-3.5 m", namedenom => "Etalxy_2000_3500", namenum => "EtaAlxy_2000_3500", what => "eta"},
	{title => "Lxy 2.5-3.0 m", namedenom => "pTlxy_2500_3000", namenum => "pTAlxy_2500_3000", what => "pt"},
	{title => "Lxy 2.5-3.0 m", namedenom => "Etalxy_2500_3000", namenum => "EtaAlxy_2500_3000", what => "eta"}
];

for (effstems) {
	# Two sets of distributions
	rebinFactor = 2;
	p9_num = (f9.Get("{namenum}_{what}1") + f9.Get("{namenum}_{what}2")).tag("lt9").rebin(rebinFactor);
	p5_num = (f5.Get("{namenum}_{what}1") + f5.Get("{namenum}_{what}2")).tag("lt5").rebin(rebinFactor);

	p9_denom = (f9.Get("{namedenom}_{what}1") + f9.Get("{namedenom}_{what}2")).tag("lt9").rebin(rebinFactor);
	p5_denom = (f5.Get("{namedenom}_{what}1") + f5.Get("{namedenom}_{what}2")).tag("lt5").rebin(rebinFactor);

	# Create the efficiency plots
	p9_eff = p9_num/p9_denom;
	p5_eff = p5_num/p5_denom;

	# Plot
	[p5_eff, p9_eff]
		.plot()
		.title("")
		.Text("{title}", {x => 0.15, y => 0.8})
		.maximum(1.0).minimum(0.0)
		.filename("eff_{namenum}_{sample}");
};
